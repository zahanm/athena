
DEFINE COMMUNITY karma USING karma_profile {
  goal
  learnable
  teachable
  student
  teacher
}

DEFINE COMMUNITY admins USING admins_profile {}

# Signup process
# --------------

DEFINE state_skill_needed FOR person DO
  PERFORM "What is the skill needed to perform that task?"
  RETURN skill
END

DEFINE state_teachable FOR person DO
  PERFORM "Tell me what you'd be interested in teaching."
  RETURN teachable
END

DEFINE identify_yourself FOR person DO
  PERFORM "Tell me your email address."
  RETURN email
END

DEFINE goal { value }

LISTEN TO public VIA http FOR goals

ON EACH goal DO
  learner = PERSON FROM goal
  learner.karma_profile = {}
  learner.karma_profile.goal = goal['value']
  response = ASK learner VIA stream TO state_skill_needed
  learner.karma_profile.learnable = {}
  skill = response[0]['skill']
  learner.karma_profile.learnable = skill
  response = ASK learner VIA stream TO state_teachable
  learner.karma_profile.teachable = {}
  teachable = response[0]['teachable']
  learner.karma_profile.teachable = teachable
  identity = ASK learner VIA stream TO identify_yourself
  learner.email = identity[0]['email']
  COMPUTE print_details ON learner
  SAVE learner TO karma
  NOTIFY public VIA stream OF learner
END

# Browse
# ------

DEFINE browse_request { value }

LISTEN TO public FOR browse_requests

ON EACH browse_request DO
  browser = PERSON FROM browse_request
  karma_users = FIND people.people FROM karma
  FOR EACH karma_user IN karma_users DO
   NOTIFY browser VIA stream OF karma_user
  END
END

# Pairing people
# --------------

# Listen for pairings made by the admins
DEFINE pairing { learner, teacher }

LISTEN TO public VIA http FOR pairings # should be 'admins'

ON EACH pairing DO
  # assuming that learner and teacher are correctly populated
  learner = pairing.learner
  teacher = pairing.teacher
  # check out the multiple uses of the profile
  learner.karma_profile.teachers = teacher
  teacher.karma_profile.students = learner
END

# external functions
# ------------------

DEFINE rank_k_teachers FOR shell ON me, teachers, k DO
  PERFORM "python ranking.py"
  RETURN teachers_list
END

# helper functions
# ----------------

DEFINE print_details ON member DO
  PRINT 'Member:'
  PRINT 'name: ' + member.first_name + " " + member.last_name
  PRINT 'email: ' + member.email
  PRINT 'goal: ' + member.karma_profile.goal
  PRINT 'learnable: ' + member.karma_profile.learnable
  PRINT 'teachable: ' + member.karma_profile.teachable
END
